# Calcolatore del circuito, dioè il data path.
# Dato il pH di una soluzione lo porta a neutralità,
# precisamente nell'intervallo [7, 8].
# Assume che il pH sia un byte codificato in virgola
# fissa con 4 bit dedicati alla parte intera.
.model DATAPATH

# Accetta cinque input: un byte rappresentante il pH
# e quattro bit di stato che determinano se è nello
# stato di reset, l'inizio del calcolo, il tipo del
# pH oppure se il pH è neutro.
# Risponde con un byte rappresentante il pH dopo il
# termine delle operazioni, ed invia due segnali di
# controllo che determinano se il pH non è accettabile
# oppure se è neutro.
.inputs  PH7 PH6 PH5 PH4 PH3 PH2 PH1 PH0 RESET INIZIO_OPERAZIONE TIPO_PH STOP_OPERAZIONE
.outputs PH_FINALE7 PH_FINALE6 PH_FINALE5 PH_FINALE4 PH_FINALE3 PH_FINALE2 PH_FINALE1 PH_FINALE0 NCLK7 NCLK6 NCLK5 NCLK4 NCLK3 NCLK2 NCLK1 NCLK0 ERRORE CONTROLLO_NEUTRO

# Valori costanti.
.subckt ZERO RES=0

# Memorizza il tipo precedente per evitare
# di creare cicli con la FSM.
.subckt REG1 A=TIPO_PH MEM=TIPO_PH_MEM

# Conta tutte le operazioni svolte per raggiungere la
# neutralità.
.subckt COUNTER RESET=RESET STOP=STOP_OPERAZIONE RES7=NCLK7 RES6=NCLK6 RES5=NCLK5 RES4=NCLK4 RES3=NCLK3 RES2=NCLK2 RES1=NCLK1 RES0=NCLK0

# Questo multiplexer riceve il segnale di stato INIZIO_OPERAZIONE
# dalla FSM; quando il segnale equivale ad UNO sceglie
# il byte inserito, altrimenti sceglie quello selezionato dal
# multiplexer di neutralità.
.subckt MUX8 SEL=INIZIO_OPERAZIONE A7=VAL7 A6=VAL6 A5=VAL5 A4=VAL4 A3=VAL3 A2=VAL2 A1=VAL1 A0=VAL0 B7=PH7 B6=PH6 B5=PH5 B4=PH4 B3=PH3 B2=PH2 B1=PH1 B0=PH0 OUT7=IN7 OUT6=IN6 OUT5=IN5 OUT4=IN4 OUT3=IN3 OUT2=IN2 OUT1=IN1 OUT0=IN0

# Quando sopraggiunge il segnale di reset, questo
# multiplexer propaga un byte di valore ZERO,
# altrimenti propaga il byte selezionato dal multiplexer
# di start.
.subckt MUX8 SEL=RESET A7=IN7 A6=IN6 A5=IN5 A4=IN4 A3=IN3 A2=IN2 A1=IN1 A0=IN0 B7=0 B6=0 B5=0 B4=0 B3=0 B2=0 B1=0 B0=0 OUT7=RST7 OUT6=RST6 OUT5=RST5 OUT4=RST4 OUT3=RST3 OUT2=RST2 OUT1=RST1 OUT0=RST0

# Questo multiplexer lascia trapelare un byte di valore
# ZERO finché non riceve il segnale di STOP_OPERAZIONE.
.subckt MUX8 SEL=STOP_OPERAZIONE A7=0 A6=0 A5=0 A4=0 A3=0 A2=0 A1=0 A0=0 B6=RST6 B5=RST5 B4=RST4 B3=RST3 B2=RST2 B1=RST1 B0=RST0 OUT7=PH_FINALE7 OUT6=PH_FINALE6 OUT5=PH_FINALE5 OUT4=PH_FINALE4 OUT3=PH_FINALE3 OUT2=PH_FINALE2 OUT1=PH_FINALE1 OUT0=PH_FINALE0

# Determina se la codifica del pH è valida.
.subckt ERROR VAL7=RST7 VAL6=RST6 VAL5=RST5 VAL4=RST4 VAL3=RST3 VAL2=RST2 VAL1=RST1 VAL0=RST0 RES=ERRORE

# Memorizza il valore del pH selezionato dal
# multiplexer di reset.
.subckt REG8 A7=RST7 A6=RST6 A5=RST5 A4=RST4 A3=RST3 A2=RST2 A1=RST1 A0=RST0 MEM7=REG7 MEM6=REG6 MEM5=REG5 MEM4=REG4 MEM3=REG3 MEM2=REG2 MEM1=REG1 MEM0=REG0

# Modifica il pH della soluzione in base al tipo che
# possiede, in altre parole se il tipo equivale a ZERO
# significa che è acida e quindi aggiunge 0.5, in caso
# contrario, se equivale a UNO, significa che è basica
# e quindi sottrae 0.25.
# Infine indirizza il risultato al multiplexer che
# verifica se il circuito si trova nello stato di
# neutralità.
.subckt MODIFIER SEL=TIPO_PH_MEM VAL7=REG7 VAL6=REG6 VAL5=REG5 VAL4=REG4 VAL3=REG3 VAL2=REG2 VAL1=REG1 VAL0=REG0 RES7=MDF7 RES6=MDF6 RES5=MDF5 RES4=MDF4 RES3=MDF3 RES2=MDF2 RES1=MDF1 RES0=MDF0

# Verifica se il pH ha raggiunto una codifica neutra.
.subckt NEUTRAL VAL7=REG7 VAL6=REG6 VAL5=REG5 VAL4=REG4 VAL3=REG3 VAL2=REG2 VAL1=REG1 VAL0=REG0 RES=CONTROLLO_NEUTRO

# Questo multiplexer riceve il segnale di stato STOP
# dalla FSM; quando il segnale equivale ad UNO sceglie
# il byte memorizzato nel registro, altrimenti sceglie
# quello modificato.
.subckt MUX8 SEL=STOP_OPERAZIONE A7=MDF7 A6=MDF6 A5=MDF5 A4=MDF4 A3=MDF3 A2=MDF2 A1=MDF1 A0=MDF0 B7=REG7 B6=REG6 B5=REG5 B4=REG4 B3=REG3 B2=REG2 B1=REG1 B0=REG0 OUT7=VAL7 OUT6=VAL6 OUT5=VAL5 OUT4=VAL4 OUT3=VAL3 OUT2=VAL2 OUT1=VAL1 OUT0=VAL0

.end

# Ricerca dei modelli ZERO, MUX8, REG1, REG8, NEUTRAL, ERROR, MODIFIER, COUNTER.
.search zero.blif
.search mux8.blif
.search reg1.blif
.search reg8.blif
.search neutral.blif
.search error.blif
.search modifier.blif
.search counter.blif
