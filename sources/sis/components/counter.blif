# Contatore da ZERO a 255, che può essere reimpostato
# a ZERO e mostra il risultato solo dopo essere stato
# interrotto.
# Il risultato è codificato in modulo.
.model COUNTER

# Accetta due input: un bit di reset ed un bit di stop.
# Risponde con un byte rappresentante il conteggio.
.inputs  RESET STOP
.outputs RES7 RES6 RES5 RES4 RES3 RES2 RES1 RES0

# Valori costanti.
.subckt ZERO RES=0
.subckt ONE  RES=1

# Quando riceve il segnale di reset, sceglie di
# propagare un byte di valore ZERO, in caso contrario
# propaga il byte selezionato dal multiplexer di stop.
.subckt MUX8 SEL=RESET A7=VAL7 A6=VAL6 A5=VAL5 A4=VAL4 A3=VAL3 A2=VAL2 A1=VAL1 A0=VAL0 B7=0 B6=0 B5=0 B4=0 B3=0 B2=0 B1=0 B0=0 OUT7=RST7 OUT6=RST6 OUT5=RST5 OUT4=RST4 OUT3=RST3 OUT2=RST2 OUT1=RST1 OUT0=RST0

# Questo multiplexer lascia trapelare un byte di valore
# ZERO finché non viene fermato dal segnale di stop.
.subckt MUX8 SEL=STOP A7=0 A6=0 A5=0 A4=0 A3=0 A2=0 A1=0 A0=0 B7=RST7 B6=RST6 B5=RST5 B4=RST4 B3=RST3 B2=RST2 B1=RST1 B0=RST0 OUT7=RES7 OUT6=RES6 OUT5=RES5 OUT4=RES4 OUT3=RES3 OUT2=RES2 OUT1=RES1 OUT0=RES0

# Questo registro memorizza l'ultimo byte che viene
# prodotto dal multiplexer di reset.
.subckt REG8 A7=RST7 A6=RST6 A5=RST5 A4=RST4 A3=RST3 A2=RST2 A1=RST1 A0=RST0 MEM7=REG7 MEM6=REG6 MEM5=REG5 MEM4=REG4 MEM3=REG3 MEM2=REG2 MEM1=REG1 MEM0=REG0

# Il sommatore legge l'ultimo valore memorizzato dal
# registro e lo somma con un byte di valore UNO, per
# poi indirizzare il risultato al multiplexer di stop.
.subckt SUM8 A7=REG7 A6=REG6 A5=REG5 A4=REG4 A3=REG3 A2=REG2 A1=REG1 A0=REG0 B7=0 B6=0 B5=0 B4=0 B3=0 B2=0 B1=0 B0=1 CIN=0 RES7=SUM7 RES6=SUM6 RES5=SUM5 RES4=SUM4 RES3=SUM3 RES2=SUM2 RES1=SUM1 RES0=SUM0 COUT=COUT

# Quando riceve il segnale di stop, sceglie di
# propagare il byte precedentemente memorizzato
# anziché la somma, viceversa quando non riceve
# il segnale, propaga il risultato della sommma.
.subckt MUX8 SEL=STOP A7=SUM7 A6=SUM6 A5=SUM5 A4=SUM4 A3=SUM3 A2=SUM2 A1=SUM1 A0=SUM0 B7=REG7 B6=REG6 B5=REG5 B4=REG4 B3=REG3 B2=REG2 B1=REG1 B0=REG0 OUT7=VAL7 OUT6=VAL6 OUT5=VAL5 OUT4=VAL4 OUT3=VAL3 OUT2=VAL2 OUT1=VAL1 OUT0=VAL0

.end

# Ricerca dei modelli ZERO, ONE, MUX8, REG8, SUM8.
.search zero.blif
.search one.blif
.search mux8.blif
.search reg8.blif
.search sum8.blif
